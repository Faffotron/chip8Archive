Nokia 3310 Template
===================
Our mission: use Octo to write games conforming to the aesthetic constraints of the Nokia 3310 cell phone! This guide will describe a number of useful routines, techniques, and tools. For general help with Octo development, consult the [GitHub repository](https://github.com/JohnEarnest/Octo#links), paying particular attention to the language manual and the FAQ page.

The template included in this directory demonstrates a simpler SCHIP-only approach to matching the necessary constraints.

Resolution and Palette
----------------------
The Nokia 3310 has a display which is 84x48 pixels. In high-resolution mode, SCHIP or XO-CHIP have a 128x64 pixel display. To conform to the proper resolution constraint, it will thus be necessary to draw a static border and clip drawing to the center of our display.

The easiest way to handle this is to use the second XO-CHIP drawing plane as a clipping "matte". We will draw the border on this plane once, at program startup, and then exclusively use the first drawing plane. In Octo's _Appearance_ panel, set the background color to `#c7f0d8`. Both foreground colors, the blend color, and the buzzer and silence colors should all be `#43523d`. By making the blend and foreground colors identical, we ensure that our "matte" is treated as opaque. By making the buzzer and silence colors identical, we suppress Octo's usual behavior of flashing the background during sound effects, and by making both of these colors match the color of our matte, we can perfectly hide the border of the true display.

Here's one way to draw our desired matte:
```
:const WIDTH  84
:const HEIGHT 48
:const TOP     8
:const LEFT   22
:calc  BOTTOM { TOP  + HEIGHT }
:calc  RIGHT  { LEFT + WIDTH  }

: block-16
	0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF
	0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF
	0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF
	0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF
: block-12
	0xFF 0xF0 0xFF 0xF0 0xFF 0xF0 0xFF 0xF0
	0xFF 0xF0 0xFF 0xF0 0xFF 0xF0 0xFF 0xF0
	0xFF 0xF0 0xFF 0xF0 0xFF 0xF0 0xFF 0xF0
	0xFF 0xF0 0xFF 0xF0 0xFF 0xF0 0xFF 0xF0

: main
	hires
	plane 2
	i  := block-16
	v1 := BOTTOM
	loop
		sprite v0 v1 0
		v0 += 16
		if v0 != 128 then
	again
	v1 := TOP
	loop
	  i  := block-16
		v0 := RIGHT
		sprite v0 v1 0
		v0 += 16
		sprite v0 v1 0
		v0 += 16
		i  := block-12
		sprite v0 v1 0
		v1 += 16
		if v1 != BOTTOM then
	again
	plane 1
```

Remember: `(22,8)` is now our logical origin for all drawing!

Full-screen bitmaps
-------------------
Suppose you've matted the display down to Nokia 3310 rez. Here's a routine for efficiently drawing a bitmap that covers the entire logical screen:
```
: nokia-blit
	v0 := LEFT
	v1 := TOP
	:calc t1 { TOP + 16 } v2 := t1
	:calc t2 { TOP + 32 } v3 := t2
	v4 := 32
	loop
		sprite v0 v1 0  i += v4
		sprite v0 v2 0  i += v4
		sprite v0 v3 0  i += v4
		v0 += 16
		:calc blit-end { LEFT + 6 * 16 }
		if v0 != blit-end then
	again
;

...

i := test-data
nokia blit
```

You can prepare a bitmap for this routine using [EZ-Pack](http://beyondloom.com/tools/ezpack.html), setting the "Sprite Size" to 0 (for SCHIP 16x16 sprites) and leaving everything else at default settings.

Note that this approach only uses four columns of pixels from the last 16x16 block, wasting a meaningful amount of storage. If space is at a premium, you could either use smaller slices (at the cost of slowing down drawing) or simply draw a centered 80x48 image instead.

Drawing Text
------------
A program will instantly feel more Nokia-ish if you use an authentic font. [EZ-Writer](http://beyondloom.com/tools/ezwriter.html) can prepare packed strings ready for inclusion in Octo programs, and includes a variable-width font based on the 3310. Here's a complete example program using a string generated in this way:
```
: nokia-font
  0x70 0xD8 0xD8 0xD8 0xF8 0xD8 0xD8 0x00 0x06 # A
  0xF0 0xD8 0xF0 0xD8 0xD8 0xD8 0xF0 0x00 0x06 # B
  0x78 0xC0 0xC0 0xC0 0xC0 0xC0 0x78 0x00 0x06 # C
  0xF0 0xD8 0xD8 0xD8 0xD8 0xD8 0xF0 0x00 0x06 # D
  0xF8 0xC0 0xF0 0xC0 0xC0 0xC0 0xF8 0x00 0x06 # E
  0xF8 0xC0 0xF0 0xC0 0xC0 0xC0 0xC0 0x00 0x06 # F
  0x70 0xC0 0xC0 0xD8 0xD8 0xD8 0x78 0x00 0x06 # G
  0xD8 0xD8 0xF8 0xD8 0xD8 0xD8 0xD8 0x00 0x06 # H
  0xC0 0xC0 0xC0 0xC0 0xC0 0xC0 0xC0 0x00 0x03 # I
  0x30 0x30 0x30 0x30 0x30 0x30 0xE0 0x00 0x05 # J
  0xCC 0xD8 0xF0 0xE0 0xF0 0xD8 0xCC 0x00 0x07 # K
  0xC0 0xC0 0xC0 0xC0 0xC0 0xC0 0xF0 0x00 0x05 # L
  0x82 0xC6 0xEE 0xFE 0xD6 0xC6 0xC6 0x00 0x08 # M
  0x8C 0xCC 0xEC 0xFC 0xDC 0xCC 0xC4 0x00 0x07 # N
  0x78 0xCC 0xCC 0xCC 0xCC 0xCC 0x78 0x00 0x07 # O
  0xF0 0xD8 0xD8 0xD8 0xF0 0xC0 0xC0 0x00 0x06 # P
  0x78 0xCC 0xCC 0xCC 0xCC 0xDC 0x78 0x0C 0x07 # Q
  0xF0 0xD8 0xD8 0xD8 0xF0 0xD0 0xD8 0x00 0x06 # R
  0x70 0xC0 0xC0 0x60 0x30 0x30 0xE0 0x00 0x05 # S
  0xFC 0x30 0x30 0x30 0x30 0x30 0x30 0x00 0x07 # T
  0xD8 0xD8 0xD8 0xD8 0xD8 0xD8 0x70 0x00 0x06 # U
  0xCC 0xCC 0xCC 0x78 0x78 0x30 0x30 0x00 0x07 # V
  0xC6 0xC6 0xD6 0xFE 0x7C 0x7C 0x6C 0x00 0x08 # W
  0xCC 0xCC 0x78 0x30 0x78 0xCC 0xCC 0x00 0x07 # X
  0xCC 0xCC 0x78 0x30 0x30 0x30 0x30 0x00 0x07 # Y
  0xF8 0x18 0x38 0x70 0xE0 0xC0 0xF8 0x00 0x06 # Z
  0x00 0x00 0x70 0x18 0x78 0xD8 0x78 0x00 0x06 # a
  0xC0 0xC0 0xF0 0xD8 0xD8 0xD8 0xF0 0x00 0x06 # b
  0x00 0x00 0x70 0xC0 0xC0 0xC0 0x70 0x00 0x05 # c
  0x18 0x18 0x78 0xD8 0xD8 0xD8 0x78 0x00 0x06 # d
  0x00 0x00 0x70 0xD8 0xF8 0xC0 0x78 0x00 0x06 # e
  0x00 0x60 0xC0 0xE0 0xC0 0xC0 0xC0 0x00 0x04 # f
  0x00 0x00 0x78 0xD8 0xD8 0x78 0x18 0x70 0x06 # g
  0xC0 0xC0 0xF0 0xD8 0xD8 0xD8 0xD8 0x00 0x06 # h
  0xC0 0x00 0xC0 0xC0 0xC0 0xC0 0xC0 0x00 0x03 # i
  0x60 0x00 0x60 0x60 0x60 0x60 0x60 0xC0 0x04 # j
  0xC0 0xC0 0xD8 0xF0 0xE0 0xF0 0xD8 0x00 0x06 # k
  0xC0 0xC0 0xC0 0xC0 0xC0 0xC0 0xC0 0x00 0x03 # l
  0x00 0x00 0xFC 0xD6 0xD6 0xD6 0xD6 0x00 0x08 # m
  0x00 0x00 0xF0 0xD8 0xD8 0xD8 0xD8 0x00 0x06 # n
  0x00 0x00 0x70 0xD8 0xD8 0xD8 0x70 0x00 0x06 # o
  0x00 0x00 0xF0 0xD8 0xD8 0xF0 0xC0 0xC0 0x06 # p
  0x00 0x00 0x78 0xD8 0xD8 0x78 0x18 0x18 0x06 # q
  0x00 0x00 0xD0 0xF0 0xC0 0xC0 0xC0 0x00 0x05 # r
  0x00 0x00 0x70 0xC0 0xF0 0x30 0xE0 0x00 0x05 # s
  0xC0 0xC0 0xE0 0xC0 0xC0 0xC0 0x60 0x00 0x04 # t
  0x00 0x00 0xD8 0xD8 0xD8 0xD8 0x78 0x00 0x06 # u
  0x00 0x00 0xD8 0xD8 0x70 0x70 0x20 0x00 0x06 # v
  0x00 0x00 0xC6 0xC6 0xD6 0x7C 0x6C 0x00 0x08 # w
  0x00 0x00 0xD8 0xD8 0x70 0xD8 0xD8 0x00 0x06 # x
  0x00 0x00 0xD8 0xD8 0xD8 0x78 0x18 0x70 0x06 # y
  0x00 0x00 0xF8 0x30 0x60 0xC0 0xF8 0x00 0x06 # z
  0x70 0xD8 0xD8 0xD8 0xD8 0xD8 0x70 0x00 0x06 # 0
  0x60 0xE0 0x60 0x60 0x60 0x60 0x60 0x00 0x04 # 1
  0xF0 0x18 0x18 0x70 0xC0 0xC0 0xF8 0x00 0x06 # 2
  0xF0 0x18 0x18 0x70 0x18 0x18 0xF0 0x00 0x06 # 3
  0x18 0x38 0x58 0x98 0xF8 0x18 0x18 0x00 0x06 # 4
  0xF0 0x80 0xF0 0x18 0x18 0x18 0xF0 0x00 0x06 # 5
  0x70 0xC0 0xF0 0xD8 0xD8 0xD8 0x70 0x00 0x06 # 6
  0xF8 0x18 0x30 0x30 0x60 0x60 0x60 0x00 0x06 # 7
  0x70 0xD8 0xD8 0x70 0xD8 0xD8 0x70 0x00 0x06 # 8
  0x70 0xD8 0xD8 0xD8 0x78 0x18 0x70 0x00 0x06 # 9
  0x00 0x00 0x00 0x00 0x00 0xC0 0xC0 0x00 0x03 # .
  0xC0 0xC0 0xC0 0xC0 0xC0 0x00 0xC0 0x00 0x03 # !
  0xF0 0x18 0x30 0x60 0x60 0x00 0x60 0x00 0x06 # ?
  0x00 0x00 0x00 0x00 0x00 0x40 0xC0 0x80 0x03 # ,
  0x80 0x80 0x00 0x00 0x00 0x00 0x00 0x00 0x02 # '
  0x00 0x00 0x00 0xF0 0x00 0x00 0x00 0x00 0x05 # -
  0xA0 0xA0 0x00 0x00 0x00 0x00 0x00 0x00 0x04 # "

:const STR_END     0xFF
:const STR_POS     0xFE
:const STR_SPACE   0xF9
:const CHAR_HEIGHT 8

:alias string-offset v3
:alias cursor-x      v4
:alias cursor-y      v5

: print-op
  if v0 == STR_POS begin
    string-offset += 2
    cursor-x := v1
    cursor-y := v2
    return
  end
  if v0 != STR_SPACE begin
    i  := long nokia-font
    v1 := v0
    v0 += v0
    v0 += v1 # *3
    i  += v0 # *3
    i  += v0 # *6
    i  += v0 # *9
    sprite cursor-x cursor-y CHAR_HEIGHT
    vf := 8
    i  += vf
    load v0
    cursor-x += v0
  else
    cursor-x += 3
  end
;
: print-string
  i := print-string-read
  save v1
  string-offset := 0
  cursor-x      := 0
  cursor-y      := 0
  loop
    0xF0 0x00 : print-string-read 0x00 0x00
    i += string-offset
    load v2
    while v0 != STR_END
    string-offset += 1
    print-op
  again
;

: test-string
  0xFE 0x24 0x1C 0x08 0x27 0x2C 0x1E 0x2B 0x2D 0xF9 0x12 0x08 0x0C 0x3E 0x3E 0x3E 0xFF


: main
	hires
	:unpack long test-string
	print-string
```

As necessary, you can customize the character set or use one of the other built-in fonts. The EZ-Writer manual includes an example of an alternate string-drawing routine for use with monospaced fonts.

Beeps and Boops
---------------
Playing music with XO-CHIP audio can get tricky, but simple monophonic sound effects are a snap. Try these on for size:
```
: play-sound
	audio
	vf := 2
	buzzer := vf
;

: sound-step    0x02 0x21 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00
: sound-stomp   0x02 0x21 0x00 0x00 0x03 0x82 0x7C 0x01 0xC0 0x00 0x00 0x00 0x00 0x00 0x00 0x00
: sound-bump    0x02 0x21 0x00 0x08 0x66 0x00 0xD8 0x01 0xB0 0x20 0x10 0x10 0x00 0x00 0x00 0x00
: sound-pickup  0xFC 0x0F 0xC0 0xFC 0x0F 0xC0 0xF8 0x1F 0x81 0xF8 0x1F 0x81 0xF0 0x3F 0x03 0xF0
: sound-heal    0xE3 0x8E 0x38 0xE3 0x8E 0x38 0xC7 0x1C 0x71 0xC7 0x1C 0x71 0xCE 0x38 0xE3 0x8E
: sound-hurt    0xA5 0xDF 0x71 0xDC 0x50 0x1D 0x38 0x97 0x5A 0x7D 0xDF 0xB5 0xE5 0xF4 0x97 0xD5
: sound-b       0xF8 0x78 0x78 0x78 0x78 0x78 0x78 0x78 0x78 0x78 0x7C 0x3C 0x3C 0x3C 0x3C 0x3C # 494hz
: sound-a-sharp 0xF8 0x78 0x3C 0x3E 0x1E 0x1F 0x0F 0x07 0x87 0x83 0xC3 0xE1 0xE0 0xF0 0xF0 0x78 # 466hz
: sound-g-sharp 0xF8 0x3E 0x0F 0x87 0xC1 0xF0 0x7C 0x3E 0x0F 0x83 0xC1 0xF0 0x7C 0x1E 0x0F 0x83 # 415hz
: sound-d-sharp 0xFE 0x07 0xF0 0x3F 0x81 0xFC 0x0F 0xC0 0x7E 0x03 0xF0 0x1F 0x81 0xFC 0x0F 0xE0 # 311hz
: sound-d       0xFE 0x03 0xF8 0x0F 0xE0 0x7F 0x01 0xFC 0x07 0xF0 0x3F 0x80 0xFE 0x03 0xF8 0x1F # 293hz
: sound-c-sharp 0xFF 0x01 0xFC 0x07 0xF8 0x0F 0xE0 0x3F 0x80 0x7F 0x01 0xFC 0x03 0xF8 0x0F 0xE0 # 277hz
: sound-c       0xFF 0x00 0xFE 0x01 0xFE 0x03 0xFC 0x03 0xF8 0x07 0xF0 0x0F 0xF0 0x1F 0xE0 0x1F # 262hz

: main
	loop
		:macro key-sfx KEY ADDR {
			vf := KEY
			if vf key begin
				i := long ADDR
				play-sound
			end
		}
		key-sfx OCTO_KEY_1 sound-step
		key-sfx OCTO_KEY_2 sound-stomp
		key-sfx OCTO_KEY_3 sound-bump
		key-sfx OCTO_KEY_4 sound-pickup
		key-sfx OCTO_KEY_Q sound-heal
		key-sfx OCTO_KEY_W sound-hurt
		key-sfx OCTO_KEY_E sound-b
		key-sfx OCTO_KEY_R sound-a-sharp
		key-sfx OCTO_KEY_A sound-g-sharp
		key-sfx OCTO_KEY_S sound-d-sharp
		key-sfx OCTO_KEY_D sound-d
		key-sfx OCTO_KEY_F sound-c-sharp
		key-sfx OCTO_KEY_Z sound-c

		vf := 4
		delay := vf
		loop
			vf := delay
			if vf != 0 then
		again
	again
```
